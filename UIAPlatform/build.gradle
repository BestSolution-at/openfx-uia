plugins {
    id "java-library"
    id "org.sonarqube" version "3.2.0"
}

sonarqube {
    properties {
        property "sonar.projectName", "JavaFX Glass Platfrom with full UIA support"
        property "sonar.projectKey", "openfx-uia"
        property "sonar.excludes", "src/main/java/com/sun/glass/ui/uia/glass/**"
    }
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar();

    toolchain {
        // since auto detection is disabled only the env var JDK8FX is picked up as java toolchain (see gradle.properties)
        // it seems not possible to query for toolchains with included JavaFX...
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '--add-exports' << 'javafx.graphics/com.sun.glass.ui=ALL-UNNAMED'
    options.compilerArgs << '--add-exports' << 'javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED'
    options.compilerArgs << '--add-exports' << 'javafx.graphics/com.sun.javafx.tk.quantum=ALL-UNNAMED'
    options.compilerArgs << '--add-exports' << 'javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED'
    options.compilerArgs << '--add-exports' << 'javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED'

}


javadoc {
    exclude 'com/sun/glass/ui/uia/**'
}


configurations {
    debugDll
}

dependencies {
    debugDll project(path: ":UIAPlatform.native", configuration: "debugRuntimeElements")

    implementation project(':UIAPlatform.core')
}

task writeServiceFiles() {
    var services = [
        "at.bestsolution.uia.IAccessibleFactory": "at.bestsolution.uia.UIAAccessibleFactory",
    ]

    services.each {
        outputs.file(layout.buildDirectory.file("services/${it.key}"))
    }
    doLast {
        outputs.files.each {
            it.text = services.get(it.name)
        }
    }
}

jar {
    dependsOn configurations.debugDll
    from configurations.debugDll.files

    manifest {
        attributes('Git-Hash':"${gitHash}")
        attributes('Git-Version': "${gitVersion}")
    }

    into("META-INF/services") {
        from tasks.writeServiceFiles
    }

}
