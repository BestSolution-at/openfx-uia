plugins {
    id "java-library"
    id "org.sonarqube" version "3.2.0"

    id "biz.aQute.bnd.builder"
}

sonarqube {
    properties {
        property "sonar.projectName", "JavaFX Glass Platfrom with full UIA support"
        property "sonar.projectKey", "openfx-uia"
        property "sonar.excludes", "src/main/java/com/sun/glass/ui/uia/glass/**"
    }
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar();

    toolchain {
        // since auto detection is disabled only the env var JDK8FX is picked up as java toolchain (see gradle.properties)
        // it seems not possible to query for toolchains with included JavaFX...
        languageVersion = JavaLanguageVersion.of(8)
    }
}


javadoc {
    exclude 'com/sun/glass/ui/uia/**'
}


configurations {
    debugDll
}

dependencies {
    debugDll project(path: ":UIAPlatform.native", configuration: "debugRuntimeElements")

    implementation project(':UIAPlatform.core')
}

task writeServiceFiles() {
    var services = [
        "com.sun.glass.ui.uia.IAccessibleFactory": "com.sun.glass.ui.uia.UIAAccessibleFactory",
    ]

    services.each {
        outputs.file(layout.buildDirectory.file("services/${it.key}"))
    }
    doLast {
        outputs.files.each {
            it.text = services.get(it.name)
        }
    }
}

jar {
    dependsOn configurations.debugDll
    from configurations.debugDll.files

    bundle {
        bnd('Bundle-SymbolicName': 'at.bestsolution.uia.UIAPlatform')
        bnd('Bundle-Version': "${semVersion}")
        bnd('Bundle-Vendor': "Bestsolution")
        bnd('-exportcontents': "javafx.uia.*,at.bestsolution.uia")
        bnd('-removeheaders':'Bnd-LastModified,Created-By,Tool')
    }

    manifest {
        attributes('Git-Hash':"${gitHash}")
        attributes('Git-Version': "${gitVersion}")
    }

    into("META-INF/services") {
        from tasks.writeServiceFiles
    }

}
